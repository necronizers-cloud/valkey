locals {
  valkey_helm_values = {
    // Global Variables
    "global.defaultStorageClass" = "local-path"
    "namespaceOverride"          = "valkey"
    "commonLabels.app"           = "valkey"

    // Primary Node Resource Limits
    "primary.resources.limits.cpu"      = "500m"
    "primary.resources.limits.memory"   = "500Mi"
    "primary.resources.requests.cpu"    = "100m"
    "primary.resources.requests.memory" = "100Mi"

    // Primary Node Pod Labels
    "primary.podLabels.component" = "pod"
    "primary.podLabels.section"   = "primary"

    // Primary Node Persistence Configuration
    "primary.persistence.size"             = "5Gi"
    "primary.persistence.labels.component" = "storage"
    "primary.persistence.labels.section"   = "primary"

    // Replica Nodes Configuration
    "replica.replicaCount" = "3"

    // Replica Nodes Resource Limits
    "replica.resources.limits.cpu"      = "500m"
    "replica.resources.limits.memory"   = "500Mi"
    "replica.resources.requests.cpu"    = "100m"
    "replica.resources.requests.memory" = "100Mi"

    // Replica Nodes Pod Labels
    "replica.podLabels.component" = "pod"
    "replica.podLabels.section"   = "replica"

    // Replica Nodes Persistence Configuration
    "replica.persistence.size"             = "5Gi"
    "replica.persistence.labels.component" = "storage"
    "replica.persistence.labels.section"   = "replica"

    // Replica Nodes Autoscaling
    "replica.autoscaling.hpa.enabled"      = true
    "replica.autoscaling.hpa.minReplicas"  = 3
    "replica.autoscaling.hpa.maxReplicas"  = 10
    "replica.autoscaling.hpa.targetCPU"    = "90"
    "replica.autoscaling.hpa.targetMemory" = "90"

    // Sentinel Configuration
    "sentinel.enabled" = true

    // Sentinel Resource Limits
    "sentinel.resources.limits.cpu"      = "500m"
    "sentinel.resources.limits.memory"   = "500Mi"
    "sentinel.resources.requests.cpu"    = "100m"
    "sentinel.resources.requests.memory" = "100Mi"

    // TLS Configuration
    "tls.enabled"         = true
    "tls.authClients"     = true
    "tls.autoGenerated"   = false
    "tls.existingSecret"  = "valkey-tls"
    "tls.certFilename"    = "tls.crt"
    "tls.certKeyFilename" = "tls.key"
    "tls.certCAFilename"  = "ca.crt"
  }
}

resource "random_password" "valkey_password" {
  length           = 16
  lower            = true
  numeric          = true
  special          = true
  override_special = "-_*"
  min_special      = 2
}

// Valkey Configuration
resource "helm_release" "valkey" {
  name      = var.valkey_configuration.name
  namespace = var.valkey_configuration.namespace

  repository       = var.valkey_configuration.repository
  chart            = var.valkey_configuration.chart
  version          = var.valkey_configuration.version
  create_namespace = var.valkey_configuration.create_namespace

  set {
    name  = "global.valkey.password"
    value = random_password.valkey_password.result
  }

  dynamic "set" {
    for_each = local.valkey_helm_values

    content {
      name  = set.key
      value = set.value
    }
  }

}
